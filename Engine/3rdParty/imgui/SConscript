import Util

Import('manager')

# We pull in glm via our overridden headers so depend on it.
depends = ['3rdParty/glm']
env = manager.CreateEnvironment(depends = depends)

Util.Copy(env, 'Include/imgui.h', 'Repo/imgui.h')
env['CPPPATH'].append('Include')
env['CPPPATH'].append(Dir('Include'))

# We have our own imconfig.h so we have to copy here to ensure that our version
# is included.
copy = [
    'imgui.cpp',
    'imgui_draw.cpp',
    'imgui_widgets.cpp',
    'imgui_internal.h',
    'imstb_rectpack.h',
    'imstb_textedit.h',
    'imstb_truetype.h',
]
for f in copy:
    Util.Copy(env, f, 'Repo/%s' % (f))

manager.AddComponent(
    name = '3rdParty/imgui',
    depends = depends,
    includePath = ['Include', Dir('Include')],
    objects = list(map(env.Object, [
        'imgui.cpp',
        'imgui_draw.cpp',
        'imgui_widgets.cpp',
    ])))
