import Util

Import('manager')

# We pull in glm via our overridden headers so depend on it.
depends = ['3rdParty/glm']
env = manager.CreateEnvironment(depends = depends)

env['CPPPATH'].append('Include')
env['CPPPATH'].append(Dir('Include'))
env['CPPDEFINES'].update({'IMGUI_IMPL': 1})

# We have our own imconfig.h so we have to copy here to ensure that our version
# is included.
copySource = [
    'imgui.cpp',
    'imgui_draw.cpp',
    'imgui_widgets.cpp',
]
copyHeader = [
    'imgui.h',
    'imgui_internal.h',
    'imstb_rectpack.h',
    'imstb_textedit.h',
    'imstb_truetype.h',
]
for f in copySource:
    Util.Copy(env, f, 'Repo/%s' % (f))
for f in copyHeader:
    Util.Copy(env, 'Include/%s' % (f), 'Repo/%s' % (f))

manager.AddComponent(
    name = '3rdParty/imgui',
    depends = depends,
    includePath = ['Include', Dir('Include')],
    objects = list(map(env.Object, [
        'imgui.cpp',
        'imgui_draw.cpp',
        'imgui_widgets.cpp',
    ])))
