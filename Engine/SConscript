import os

Import('manager')

# Base engine. Currently these parts of the engine have inter-dependencies which
# means they can't be built individually, so we build them all into 1 "base"
# component.
baseComponents = [
    'Engine',
    'GPU',
    'Input',
    'Render',
]

# Remaining components making up the full engine.
extraComponents = [
]

allComponents = ['Core'] + baseComponents + extraComponents

manager.baseEnv['CPPPATH'] += [manager.SourceDir('Runtime')]

# Add all 3rd party library components.
SConscript(dirs = ['3rdParty'])

################
# Core library #
################

SConscript(dirs = ['Runtime/Core'])

#############
# Tools #
#############

SConscript(dirs = ['Tools'])

###############
# Base engine #
###############

depends = [
    'Engine/Core',
    '3rdParty/imgui',
    '3rdParty/RapidJSON',
    '3rdParty/SPIRV-Reflect',
    '3rdParty/VulkanMemoryAllocator',
    'External/shaderc',
]

if manager.baseEnv['PLATFORM'] == 'Linux':
    depends += ['External/XCB']

env = manager.CreateEnvironment(depends = depends)
Export('env')

manager.AddComponent(
    name = 'Engine/Base',
    depends = depends,
    objects = SConscript(dirs = [os.path.join('Runtime', c) for c in baseComponents]))

#########################
# Additional components #
#########################

depends = [
    'Engine/Base',
]

env = manager.CreateEnvironment(depends = depends)
Export('env')

# TODO: These should all be built independently eventually.
manager.AddComponent(
    name = 'Engine',
    depends = depends,
    objects = [])
# SConscript(dirs = [os.path.join('runtime', c) for c in extraComponents]))
