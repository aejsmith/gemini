import os

Import('manager')

dummyEnv = Environment(ENV = os.environ)
dummyEnv['CPPPATH'] = []
dummyEnv['LIBPATH'] = []
dummyEnv['LIBS'] = []

def AddExternalComponent(name, env):
    manager.AddComponent(
        name = name,
        includePath = env['CPPPATH'],
        libPath = env['LIBPATH'],
        libs = env['LIBS'])

# SDL.
sdlEnv = dummyEnv.Clone()
sdlEnv.ParseConfig('sdl2-config --cflags --libs')
AddExternalComponent('External/SDL', sdlEnv)

# XCB.
xcbEnv = dummyEnv.Clone()
xcbEnv.ParseConfig('pkg-config --cflags --libs x11-xcb')
AddExternalComponent('External/XCB', xcbEnv)

# libclang.
llvmEnv = dummyEnv.Clone()
llvmEnv.ParseConfig('llvm-config --cflags --ldflags')
llvmEnv['LIBS'] = ['clang']
AddExternalComponent('External/libclang', llvmEnv)
